
buildscript {
    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.9"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
}

group = 'de.infonautika.postman'
version = '0.0.7'

allprojects {
    repositories {
        jcenter()
    }
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'com.gradle.plugin-publish'
    apply plugin: 'java'
}

apply plugin: 'java'

sourceCompatibility = 1.8
targetCompatibility = 1.8


dependencies {
    compile gradleApi()
    compile 'com.moowork.gradle:gradle-node-plugin:1.2.0'
    compile 'org.apache.commons:commons-io:1.3.2'
    compile 'com.google.code.gson:gson:2.8.0'
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        PostmanRunnerPublication(MavenPublication) {
            from components.java
            groupId 'de.infonautika.postman'
            artifactId 'postman-runner'
            artifact sourcesJar
            version "${project.version}"
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['PostmanRunnerPublication']

    dryRun = false
    publish = true

    pkg {
        repo = 'maven'
        name = 'postmanrunner'
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/simomat/gradle-postman-runner'
        issueTrackerUrl = 'https://github.com/simomat/gradle-postman-runner/issues'
        vcsUrl = 'https://github.com/simomat/gradle-postman-runner.git'
        labels = ['postman', 'newman', 'gradle']
        githubRepo = 'simomat/gradle-postman-runner'
        githubReleaseNotesFile = 'README.md'

        version {
            name = "${project.version}"
            desc = 'Gradle plugin to run postman'
            released  = new Date()
            vcsTag = '0.0.7'
        }
    }
}

pluginBundle {
    website = 'https://github.com/simomat/gradle-postman-runner'
    vcsUrl = 'https://github.com/simomat/gradle-postman-runner.git'
    description = 'Gradle plugin to run postman'
    tags = ['postman', 'newman']

    plugins {
        postmanRunner {
            id = 'de.infonautika.postman'
            displayName = 'Gradle plugin to run postman'
        }
    }
}
