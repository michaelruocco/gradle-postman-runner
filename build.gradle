plugins {
    id "groovy"
    id "java-gradle-plugin"
    id "com.gradle.plugin-publish" version "0.13.0"
    id "com.diffplug.spotless" version "5.11.1"
    id "pl.allegro.tech.build.axion-release" version "1.13.1"
    id "com.github.ben-manes.versions" version "0.38.0"
    id "nebula.lint" version "16.17.1"
}

group = "com.github.michaelruocco"

scmVersion {
    versionCreator "versionWithBranch"
    tag {
        prefix = "gradle-postman-runner-"
    }
}
project.version = scmVersion.version

repositories {
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
}

dependencies {
    compile "com.moowork.gradle:gradle-node-plugin:1.3.1"
    compile "org.apache.commons:commons-io:1.3.2"
    compile "com.google.code.gson:gson:2.8.6"

    testCompile "org.assertj:assertj-core:3.19.0"
    testCompile "com.github.tomakehurst:wiremock-jre8:2.27.2"
    testCompile("org.spockframework:spock-core:1.1-groovy-2.4") {
        exclude group: "org.codehaus.groovy"
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

def gitUrl = "https://github.com/michaelruocco/gradle-postman-runner"
pluginBundle {
    website = gitUrl
    vcsUrl = "${gitUrl}.git"
    description = "Gradle plugin to run postman"
    tags = ["postman", "newman"]

    plugins {
        gradlePostmanRunner {
            id = "com.github.michaelruocco.gradle-postman-runner"
            displayName = "Gradle plugin to run postman"
        }
    }
}

spotless {
    groovy {
        importOrder()
    }
    java {
        removeUnusedImports()
        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()

        replace 'Not enough space after if', 'if(', 'if ('
        replaceRegex 'Too much space after if', 'if +\\(', 'if ('

        replace 'Not enough space after for', 'for(', 'for ('
        replaceRegex 'Too much space after for', 'for +\\(', 'for ('

        replace 'Not enough space after while', 'while(', 'while ('
        replaceRegex 'Too much space after while', 'while +\\(', 'while ('
    }
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}